apply plugin: 'com.android.library'
apply plugin: 'maven'


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'

    }
}

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return Integer.parseInt(formattedDate)
}
String releaseType = "mainlineRelease"
String artifactId = "android-sdk"
android {
    compileSdkVersion 19
    buildToolsVersion "20"
    useOldManifestMerger false
    defaultPublishConfig "mainlineRelease"

    String url = "\"\"";
    String configurationUrl = "\"\"";
    try {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        if (properties.stringPropertyNames().contains('mp.url')) {
            url = properties.getProperty('mp.url')
        }
        if (properties.stringPropertyNames().contains('mp.configurl')) {
            configurationUrl = properties.getProperty('mp.configurl')
        }
        if (properties.stringPropertyNames().contains('mp.releasetype')) {
            releaseType = properties.getProperty('mp.releasetype')
            if (releaseType.contains("kahuna")){
                defaultPublishConfig "kahunaRelease"
                artifactId = "android-sdk-kahuna"
            }
        }
    }catch (Exception e){

    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionName sdkversion
        versionCode getDate()
        testApplicationId "com.mparticle.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testHandleProfiling true
        testFunctionalTest true

        buildConfigField "String", "MP_URL", url
        buildConfigField "String", "MP_CONFIG_URL", configurationUrl

    }

    productFlavors {
        mainline {

        }

        kahuna {

        }
    }

    buildTypes {

        debug {
            runProguard false
        }

        unity {
            runProguard true
            proguardFile 'proguard.txt'
        }

        release {
            runProguard true
            proguardFile 'proguard.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

}

android.libraryVariants.all { variant ->
    def name = variant.name

    task "javadoc$name"(type: Javadoc) {
        description = "Generates javadoc for build $name"
        destinationDir = new File('docs', variant.baseName)
        source = files(variant.javaCompile.source)
        classpath = files(android.getBootClasspath(), variant.javaCompile.classpath)
        title = null
        ext.androidJar =
                "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        List<File> pathList = new ArrayList<File>();
        pathList.add(new File("docs/doclava-1.0.6.jar"))
        options.addStringOption('title', 'Android SDK')
        options.addStringOption('templatedir', 'docs/customization')
        options.docletpath = pathList
        options.doclet = "com.google.doclava.Doclava"
        exclude '**/R.java'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (mavenrepo == "s3") {
                configuration = configurations.deployerJars
                repository(url: "s3://maven.mparticle.com")
                pom.version = sdkversion
                pom.artifactId = artifactId
                pom.groupId = 'com.mparticle'
                pom.packaging = 'jar'
            }else{
                repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
                pom.version = sdkversion
                pom.artifactId = artifactId
                pom.groupId = 'com.mparticle'
                pom.packaging = 'jar'
            }
        }
    }
}

dependencies {
    deployerJars "org.kuali.maven.wagons:maven-s3-wagon:1.2.1"
    kahunaCompile files('embeddedkits/Kahuna489.jar')
    compile files('embeddedkits/KochavaSDK.jar')
    compile files('embeddedkits/MobileAppTracker-3.1.jar')
    provided 'com.google.android.gms:play-services:4.+'
    unityCompile project('unityskeleton')
}
