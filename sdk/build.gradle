apply plugin: 'android-library'
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'

    }
}

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionName sdkversion
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "ECHO", "true"
            runProguard false
        }

        release {
            buildConfigField "boolean", "ECHO", "false"
            runProguard false
            proguardFile 'proguard.txt'
        }

    }

    lintOptions {
        abortOnError false
    }

}

android.libraryVariants.all { variant ->
    def name = variant.name

    task "javadoc$name"(type: Javadoc) {
        description = "Generates javadoc for build $name"
        destinationDir = new File('docs', variant.baseName)
        source = 'src/main/java'
        classpath = files(android.plugin.runtimeJarList, variant.javaCompile.classpath)
        title = null
        List<File> pathList = new ArrayList<File>();
        pathList.add(new File("docs/doclava-1.0.6.jar"))
        options.addStringOption('title', 'Android SDK')
        options.addStringOption('templatedir', 'docs/customization')
        options.docletpath = pathList
        options.doclet = "com.google.doclava.Doclava"
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.BuilderConstants.DEBUG)) {
        return; // Skip debug builds.
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (mavenrepo == "s3") {
                configuration = configurations.deployerJars
                repository(url: "s3://www.mparticle.com/maven")
                pom.version = sdkversion
                pom.artifactId = 'android-sdk'
                pom.groupId = 'com.mparticle'
                pom.packaging = 'aar'
            }else{
                repository(url: "file://localhost/Users/sdozor/maven/repo/")
                pom.version = sdkversion
                pom.artifactId = 'android-sdk'
                pom.groupId = 'com.mparticle'
            }
        }
    }
}

dependencies {
    deployerJars "org.kuali.maven.wagons:maven-s3-wagon:1.2.1"
}
