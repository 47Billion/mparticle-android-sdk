import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'android'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'http://www.bugsense.com/gradle/' }
    maven { url 'http://download.crashlytics.com/maven' }
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    def gitHash = "git rev-parse --short HEAD".execute().text.trim()

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        buildConfigField "String", "GIT_SHA", "\"$gitHash\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        versionCode 1
    }

    productFlavors {
        googleanalytics {
            packageName 'com.mparticle.particlebox.googleanalytics'
        }
        mparticle {

        }
        mixpanel {
            packageName 'com.mparticle.particlebox.mixpanel'
        }
        flurry {
            packageName 'com.mparticle.particlebox.flurry'
        }
        urbanairship {
            packageName 'com.mparticle.particlebox.urbanairship'
        }
        bugsense {
            packageName 'com.mparticle.particlebox.bugsense'
        }
        kahuna {
            packageName 'com.mparticle.particlebox.kahuna'
        }
        crashlytics {
            packageName 'com.mparticle.particlebox.crashlytics'
        }
    }
}

dependencies {
	compile 'com.android.support:support-v4:19.0.0'
	compile 'com.android.support:appcompat-v7:19.0.0'
    compile project(':sdk')
    googleanalyticsCompile files("libs/libGoogleAnalyticsServices.jar")
    mixpanelCompile 'com.mixpanel.android:mixpanel-android:4.0.0-RC3@aar'
    flurryCompile files ("libs/FlurryAnalytics_3.3.3.jar")
    urbanairshipCompile files ("libs/urbanairship-lib-3.2.3.jar")
    bugsenseCompile 'com.bugsense.trace:bugsense:3.5'
    kahunaCompile files ("libs/Kahuna.jar")
    crashlyticsCompile 'com.crashlytics.android:crashlytics:1.+'
}